PREFIX a-popis-dat-pojem: <http://onto.fel.cvut.cz/ontologies/slovník/agendový/popis-dat/pojem/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

CONSTRUCT {
    ?pojem a ?typ ;
        a-popis-dat-pojem:je-pojmem-ze-slovníku ?slovnik .
    ?typ rdfs:label ?labelTypu .
    ?slovnik rdfs:label ?labelSlovniku .
} WHERE {
	?pojem a a-popis-dat-pojem:term ;
       	a-popis-dat-pojem:je-pojmem-ze-slovníku ?slovnik .
    OPTIONAL {
   	    ?slovnik rdfs:label ?labelS .
   	}
  	BIND(COALESCE(?labelS,str(?slovnik)) as ?labelSlovniku)

    OPTIONAL {
        ?pojem a ?t .
    	FILTER(!(?t IN (a-popis-dat-pojem:term,skos:Concept,owl:Class,owl:DatatypeProperty,owl:NamedIndividual,owl:ObjectProperty)))
  	    OPTIONAL {
                ?t rdfs:label ?lT .
        }
    	BIND(COALESCE(?lT,str(?t)) as ?labelTypu)
    }
    BIND(COALESCE(?t,a-popis-dat-pojem:not-filled) as ?typ)
}
