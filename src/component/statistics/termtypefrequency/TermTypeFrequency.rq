PREFIX a-popis-dat-pojem: <http://onto.fel.cvut.cz/ontologies/slovnik/agendovy/popis-dat/pojem/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
	?pojem a ?typ ;
       	a-popis-dat-pojem:je-pojmem-ze-slovniku ?slovnik .
   	?typ rdfs:label ?labelTypu .
  	?slovnik rdfs:label ?labelSlovniku .
} WHERE {
	?pojem a a-popis-dat-pojem:term ;
       	a-popis-dat-pojem:je-pojmem-ze-slovniku ?slovnik .
   	?slovnik rdfs:label ?labelSlovniku .
  	FILTER(lang(?labelSlovniku) = "cs")
    OPTIONAL {
        ?pojem a ?t .
    	FILTER(!(?t IN (a-popis-dat-pojem:term,skos:Concept)))
      	SERVICE silent <http://onto.fel.cvut.cz/rdf4j-server/repositories/ufo-current> {
        	?t rdfs:label ?lT .
    	    FILTER(lang(?lT) = "cs")
  	    }
    	BIND(COALESCE(?lT,str(?t)) as ?labelT)
    }
    BIND(COALESCE(?t,a-popis-dat-pojem:not-filled) as ?typ)
    BIND(COALESCE(?labelT,"Not Filled"@cs) as ?labelTypu)
}